buildscript {
    repositories {
        mavenCentral()
    }

    configurations.maybeCreate('pitest')

    dependencies {
        pitest 'org.pitest:pitest-junit5-plugin:0.8'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'info.solidsoft.pitest' version '1.4.5'
    id 'org.springframework.boot' version '2.1.7.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.1.11"
}

sourceCompatibility = 11
targetCompatibility = 11

def lombokVersion = '1.18.10'
def vavrVersion = '0.10.2'
def cucumberVersion = '4.7.2'
def swaggerVersion = '2.9.+'
def junitVersion = '5.5.0'
def mockitoVersion = '2.+'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    compile "io.vavr:vavr:$vavrVersion"

    implementation 'org.springframework.boot:spring-boot-starter-web'

    compile "io.springfox:springfox-swagger2:$swaggerVersion"
    compile "io.springfox:springfox-swagger-ui:$swaggerVersion"
    compile "io.springfox:springfox-bean-validators:$swaggerVersion"

    testImplementation "io.cucumber:cucumber-java8:$cucumberVersion"
    testImplementation "io.cucumber:cucumber-junit:$cucumberVersion"
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"

    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"

}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

cucumberReports {
    outputDir = file('build/reports/cucumber')
    buildId = '0'
    reports = files('build/reports/cucumber/cucumber.json')
}


test {
    useJUnitPlatform()
}

pitest {
    testPlugin = 'junit5'
    targetClasses = ['lv.starub.agilestacks.template.validation.*']
    threads = 4
    outputFormats = ['HTML']
    timestampedReports = false
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--plugin', 'json:build/reports/cucumber/cucumber.json', '--glue', 'lv.starub.agilestacks.template.validation', 'src/test/resources']
        }
    }
}

generateCucumberReports.dependsOn cucumber

bootJar {
    mainClassName = 'lv.starub.agilestacks.template.validation.Application'
}
